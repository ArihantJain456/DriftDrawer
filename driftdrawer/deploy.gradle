apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply from: 'keystore.gradle'//the file, containing the bintray API key

ext {
    groupId = 'com.sdsmdg.aridj'
    artifactId = 'driftdrawer'
    libVersion = '1.0.0'
}

version = libVersion
group = groupId

bintray {
    user = 'arihantjain456'
    key = bintray_key // Defined in keystore.gradle file
    configurations = ['archives']
    publish = false //[Default: false] Whether version should be auto published after an upload
    override = false

    pkg {
        repo = "DriftDrawer"
        name = "driftdrawer"
        version {
            name = libVersion
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId groupId
            artifactId artifactId
            version libVersion
            name artifactId
        }
    }
}

if (project.hasProperty("kotlin")) { //Kotlin libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc, dependsOn: dokka) {

    }
} else if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
//     options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}